import "salloc.hob" as salloc;
import "io.hob" as io;
import "fs.hob" as fs;

public struct String {
    public buffer: []u8,
    cap: usize 
}

public fun new() -> String {
    var result: String;
    result.buffer.length = 0;
    result.cap = 0;
    return result;
}

public fun with_cap(cap: usize) -> String {
    var result = new();
    result.reserve(cap);
    return result;
}

public fun from_slice(slice: []u8) -> String {
    var result = with_cap(slice.length);
    result.append_slice(slice);
    return result;
}

public fun(*String) reserve(cap: usize) -> void {
    if self.*.cap >= cap {
        return;
    }
    if self.*.cap == 0 {
        self.*.buffer.raw = salloc.allocate(cap) as *u8;
    } else {
        self.*.buffer.raw = salloc.reallocate(self.*.buffer.raw as *void, cap) as *u8;
    }
    self.*.cap = cap;
}

public fun(String) length() -> usize {
    return self.buffer.length;
}

public fun(String) calc_cap(requires: usize) -> usize {
    var new_cap = self.cap * 2;
    if requires >= new_cap {
        new_cap = requires;
    }
    return new_cap;
}

public fun(*String) maybe_extend(requires: usize) -> void {
    if requires > self.*.cap {
        self.*.reserve(self.*.calc_cap(self.*.length() + 1));
    }
}

public fun(*String) push(char: u8) -> void {
    self.*.maybe_extend(self.*.length() + 1);
    self.*.buffer[self.*.length()] = char;
    self.*.buffer.length = self.*.length() + 1;
}

public fun(*String) append_slice(slice: []u8) -> void {
    var i: usize = 0;
    self.*.maybe_extend(self.*.length() + slice.length);
    while i < slice.length {
        self.*.push(slice[i]);
        i = i + 1;
    }
}

public fun(*String) append(other: *String) -> void {
    self.*.append_slice(other.*.buffer);
}

const READ_FROM_BUFFER_SIZE: usize = 64;

public fun(*String) append_fd(fd: fs.File) -> void {
    var buf: [READ_FROM_BUFFER_SIZE]u8;
    while true {
        var buf_slice = &buf as []u8;
        buf_slice.length = io.read_from(fd, &buf_slice) as usize;
        self.*.append_slice(buf_slice);
        if buf_slice.length != READ_FROM_BUFFER_SIZE {
            break;
        }
    }
}

public fun(String) free() -> void {
    if self.cap != 0 {
        salloc.free(self.buffer.raw as *void);
    }
}

public fun(String) print() -> void {
    io.print(self.buffer);
}
